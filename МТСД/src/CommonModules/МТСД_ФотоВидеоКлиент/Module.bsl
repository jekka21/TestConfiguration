#Если МобильныйКлиент Тогда  
	
#Область ПрограммныйИнтерфейс

//Сделать Фото
//
//Возвращаемое значение: 
//	ДанныеМультимедиа
//
Функция СделатьФото(НастройкиФотографирования = Неопределено) Экспорт

	Если СредстваМультимедиа.ПоддерживаетсяФотоснимок() Тогда
		Если НастройкиФотографирования = Неопределено Тогда 
			НастройкиФотографирования = НовыеНастройкиФотографирования();		
		КонецЕсли;
		ДанныеМультимедиа = СредстваМультимедиа.СделатьФотоснимок(НастройкиФотографирования.ТипКамеры, НастройкиФотографирования.Разрешение, НастройкиФотографирования.Качество, , , НастройкиФотографирования.Отметка, );
	Иначе
		Сообщение = Новый СообщениеПользователю();    
		Сообщение.Текст = "Невозможно сделать фото";
		Сообщение.Сообщить();
	КонецЕсли;
	
	Возврат ДанныеМультимедиа;

КонецФункции

//Новые настройки фотографирования
//
//Возвращаемое значение: Настройки
//
//
Функция НовыеНастройкиФотографирования() Экспорт
	
	Настройки = Новый Структура;
	Настройки.Вставить("ТипКамеры", ТипКамерыУстройства.Авто);
	Настройки.Вставить("Разрешение", Новый РазрешениеКамерыУстройства);
	Настройки.Вставить("Качество", 100);       
	Настройки.Вставить("Отметка", Новый ОтметкаНаФотоснимке(Истина, "ДФ='dd.MM.yyyy ЧЧ:мм'", ""));
	
	Возврат Настройки;
	
КонецФункции
 
//Сделать видео
//
//Возвращаемое значение: 
//	ДанныеМультимедиа
//
Функция СделатьВидео(НастройкиВидеозаписи = Неопределено) Экспорт

	Если СредстваМультимедиа.ПоддерживаетсяВидеозапись() Тогда 
		Если НастройкиВидеозаписи = Неопределено Тогда 
			НастройкиВидеозаписи = НовыеНастройкиВидеозаписи();
		КонецЕсли;
		ДанныеМультимедиа = СредстваМультимедиа.СделатьВидеозапись(НастройкиВидеозаписи.ТипКамеры, НастройкиВидеозаписи.КачествоВидеозаписи);
	Иначе
		Сообщение = Новый СообщениеПользователю();    
		Сообщение.Текст = "Невозможно записать видео";
		Сообщение.Сообщить();	
	КонецЕсли;		          
	
	Возврат ДанныеМультимедиа;
		
КонецФункции

// Новые настройки видеозаписи
// 
// Возвращаемое значение:
//  Структура 
//
Функция НовыеНастройкиВидеозаписи() Экспорт
	
	Настройки = Новый Структура;
	Настройки.Вставить("ТипКамеры", ТипКамерыУстройства.Авто);
	Настройки.Вставить("КачествоВидеозаписи", КачествоВидеозаписи.Высокое);
	Возврат Настройки;
	
КонецФункции

#КонецОбласти

#КонецЕсли

