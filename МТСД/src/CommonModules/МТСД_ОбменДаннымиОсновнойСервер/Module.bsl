
#Область ПодпискиНаСобытия

Процедура СправочникОбъектПередЗаписью(Объект, Отказ) Экспорт
	
	ОбъектПередЗаписью(Объект, Отказ);
	
КонецПроцедуры

Процедура ДокументОбъектПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	ОбъектПередЗаписью(Объект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область РегламентныеЗадания

Процедура ВыполнитьПостОбработкуПослеПолученияДанныхОтМТСД() Экспорт
	
	// TODO Если фоновое задание выполняется, то возврат.
	
	РегистрыСведений.МТСД_ОбъектыДляПостобработкиПослеПолученияДанныхОтМТСД.ВыполнитьПостОбработку();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедураФункции

Процедура ОбъектПередЗаписью(Объект, Отказ)
	
	Если Объект.ОбменДанными.Загрузка Тогда
		// TODO При загрузке данных из внешних источников надо зарегистрирвоать изменения для МТСД
		Возврат;
	КонецЕсли;
	
	Если НЕ ЕстьИзмененияДляАвтономнойКонфигурации(Объект) Тогда
		УдалитьУзлыМТСДИзПолучателей(Объект);
	КонецЕсли;
	
КонецПроцедуры

Функция ЕстьИзмененияДляАвтономнойКонфигурации(Объект)
	
	РеквизитыАвтономнойКонфигурации = РеквизитыАвтономнойКонфигурации(Объект);
	Если НЕ ЗначениеЗаполнено(РеквизитыАвтономнойКонфигурации) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого ИмяРеквизита из РеквизитыАвтономнойКонфигурации Цикл
		Если Объект[ИмяРеквизита] <> Объект.Ссылка[ИмяРеквизита] Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция РеквизитыАвтономнойКонфигурации(Объект)
	
	// всегда Метаданные.СоставАвтономнойКонфигурации = Неопределено
	// в связи с этим приходится РеквизитыАвтономнойКонфигурации заполнять кодом.
	
	РеквизитыАвтономнойКонфигурации = Новый Массив;
	Если ТипЗнч(Объект) = Тип("СправочникОбъект.Номенклатура") Тогда
		РеквизитыАвтономнойКонфигурации.Добавить("Артикул");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РеквизитыАвтономнойКонфигурации) Тогда
		Возврат РеквизитыАвтономнойКонфигурации;
	КонецЕсли;
	
	МетаданныеОбъекта = Объект.Метаданные();
	ПолноеИмя = МетаданныеОбъекта.ПолноеИмя();
	
	Если СтрНачинаетсяС(ПолноеИмя, "Справочник") Тогда
		Если МетаданныеОбъекта.ДлинаНаименования > 0 Тогда
			РеквизитыАвтономнойКонфигурации.Добавить("Наименование");
		КонецЕсли;
		
		Если МетаданныеОбъекта.ДлинаКода > 0 Тогда
			РеквизитыАвтономнойКонфигурации.Добавить("Код");
		КонецЕсли;
		
		Если МетаданныеОбъекта.Иерархический Тогда
			РеквизитыАвтономнойКонфигурации.Добавить("Родитель");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(МетаданныеОбъекта.Владельцы) Тогда
			РеквизитыАвтономнойКонфигурации.Добавить("Владелец");
		КонецЕсли;
		
		РеквизитыАвтономнойКонфигурации.Добавить("ПометкаУдаления");
		
	ИначеЕсли СтрНачинаетсяС(ПолноеИмя, "Документ") Тогда
		РеквизитыАвтономнойКонфигурации.Добавить("Номер");
		РеквизитыАвтономнойКонфигурации.Добавить("Дата");
		РеквизитыАвтономнойКонфигурации.Добавить("ПометкаУдаления");
		
		Если МетаданныеОбъекта.Проведение = Метаданные.СвойстваОбъектов.Проведение.Разрешить Тогда
			РеквизитыАвтономнойКонфигурации.Добавить("Проведен");
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Процедура УдалитьУзлыМТСДИзПолучателей(Объект)
	
	ПолучателиКУдалению = Новый Массив;
	
	Для каждого Получатель Из Объект.ОбменДанными.Получатели Цикл
		Если ТипЗнч(Получатель) = Тип("ПланОбменаСсылка.МТСД") Тогда
			ПолучателиКУдалению.Добавить(Получатель);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Получатель Из ПолучателиКУдалению Цикл
		Объект.ОбменДанными.Получатели.Удалить(Получатель);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
