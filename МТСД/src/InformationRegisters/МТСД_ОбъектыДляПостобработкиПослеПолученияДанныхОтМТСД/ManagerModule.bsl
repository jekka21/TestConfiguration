
#Область ПрограммныйИнтерфейс

Процедура ЗарегистрироватьОбъект(Объект, УзелМТСД) Экспорт
	
	МенеджерЗаписи = НовыйМенеджерЗаписи(Объект, УзелМТСД, Ложь);
	МенеджерЗаписи.ДатаПолученияОбъекта = ТекущаяДатаСеанса();
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура УдалитьРегистрациюОбъекта(Объект, УзелМТСД) Экспорт
	
	МенеджерЗаписи = НовыйМенеджерЗаписи(Объект, УзелМТСД);
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Удалить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистирироватьОшибкуОбрабокти(Объект, УзелМТСД, ТекстОшибки, ВыполнятьПовторнуюОбработкуПослеОшибки = Ложь)
	
	МенеджерЗаписи = НовыйМенеджерЗаписи(Объект, УзелМТСД);
	МенеджерЗаписи.ЕстьОшибкаПостОбработки = Истина;
	МенеджерЗаписи.ВыполнятьПовторнуюОбработкуПослеОшибки = ВыполнятьПовторнуюОбработкуПослеОшибки;
	МенеджерЗаписи.ТекстОшибки = ТекстОшибки;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ВыполнитьПостОбработку() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ * ИЗ РегистрСведений.МТСД_ОбъектыДляПостобработкиПослеПолученияДанныхОтМТСД
	|ГДЕ НЕ ЕстьОшибкаПостОбработки ИЛИ ВыполнятьПовторнуюОбработкуПослеОшибки";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МенеджерОбъекта = МТСД_БСП_ОбщегоНазначения.МенеджерОбъектаПоСсылке(Выборка.Объект);
		
		ТекстОшибки = ""; ВыполнятьПовторнуюОбработкуПослеОшибки = Ложь;
		МенеджерОбъекта.ВыполнитьПостОбработкуПослеПолученияДанныхОтМТСД(Выборка.Объект, ТекстОшибки, ВыполнятьПовторнуюОбработкуПослеОшибки);
		
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			МенеджерЗаписи.ЕстьОшибкаПостОбработки = Истина;
			МенеджерЗаписи.ВыполнятьПовторнуюОбработкуПослеОшибки = ВыполнятьПовторнуюОбработкуПослеОшибки;
			МенеджерЗаписи.ТекстОшибки = ТекстОшибки;
			МенеджерЗаписи.Записать();
		Иначе
			МенеджерЗаписи.Удалить();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Функция НовыйМенеджерЗаписи(Объект, УзелМТСД, Прочитать = Истина)
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Объект = Объект;
	МенеджерЗаписи.УзелМТСД = УзелМТСД;
	
	Если Прочитать Тогда
		МенеджерЗаписи.Прочитать();
	КонецЕсли;
	
	Возврат МенеджерЗаписи;
	
КонецФункции

#КонецОбласти
