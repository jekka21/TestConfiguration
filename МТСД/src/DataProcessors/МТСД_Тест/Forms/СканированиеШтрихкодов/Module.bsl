
#Область СканироватьОдинШтрихкод

&НаКлиенте
Процедура СканироватьОдинШтрихкод(Команда)
	
	ОповещениеСканирования = Новый ОписаниеОповещения("ПослеСканированияОдногоШтрихкода", ЭтаФорма);
	МТСД_СканерШтрихкодовКлиент.СканироватьШтрихкоды(ОповещениеСканирования, , "Сканируйте один штрихкод");
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСканированияОдногоШтрихкода(Штрихкод, Результат, Сообщение, ДополнительныеПараметры) Экспорт

	Если НЕ ЗначениеЗаполнено(Штрихкод) Тогда
		Возврат;
	КонецЕсли;

	ЭтоКорректныйШтрихкод = Истина; // Проверка корректности штрихкода.
	
	Если ЭтоКорректныйШтрихкод Тогда
		Сообщить("Просканирован штрихкод."); // Действия по обработке корректного штрихкода.
	Иначе
		Сообщение = "Сообщение об ошибке.";
	КонецЕсли;
	МТСД_СканерШтрихкодовКлиент.ПослеСканированияОдногоШтрихкода(Результат, Сообщение);
	
КонецПроцедуры

#КонецОбласти

#Область СканироватьРазныеШтрихкоды

&НаКлиенте
Процедура СканироватьРазныеШтрихкоды(Команда)
	
	ДополнительныеПараметры = Новый Структура("ПросканированныеШтрихкоды", Новый Массив);
	
	ОповещениеСканирования = Новый ОписаниеОповещения("ПослеСканированияСледующегоШтрихкода", ЭтаФорма, ДополнительныеПараметры);
	ОбработчикЗакрытия = Новый ОписаниеОповещения("ПослеЗакрытияОкнаСканированияШтрихкода", ЭтаФорма, ДополнительныеПараметры);
	
	МТСД_СканерШтрихкодовКлиент.СканироватьШтрихкоды(ОповещениеСканирования, ОбработчикЗакрытия, "Сканируйте различные штрихкоды");

КонецПроцедуры

&НаКлиенте
Процедура ПослеСканированияСледующегоШтрихкода(Штрихкод, Результат, Сообщение, ДополнительныеПараметры) Экспорт

	Если НЕ ЗначениеЗаполнено(Штрихкод) Тогда
		Возврат;
	КонецЕсли;
		
	Если ДополнительныеПараметры.ПросканированныеШтрихкоды.Найти(Штрихкод) = Неопределено Тогда
		ДополнительныеПараметры.ПросканированныеШтрихкоды.Добавить(Штрихкод);
		Результат = Истина;
		Сообщение = "Просканирован новый штрихкод.";
	Иначе 
		Результат = Ложь;
		Сообщение = "Штрихкод просканирован повторно.";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияОкнаСканированияШтрихкода(ДополнительныеПараметры) Экспорт

	
	Если ЗначениеЗаполнено(ДополнительныеПараметры.ПросканированныеШтрихкоды) Тогда
		ТекстСообщения = "Просканированы штрихкоды:
		|" + СтрСоединить(ДополнительныеПараметры.ПросканированныеШтрихкоды, Символы.ПС);
	Иначе
		ТекстСообщения = "Штрихкоды не были просканированы.";
	КонецЕсли;
	
	Сообщить(ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти